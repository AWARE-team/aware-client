
apply plugin: 'com.android.application'
apply plugin: 'com.jfrog.bintray'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.aware.phone"
        versionCode version_code
        versionName version_readable
        targetSdkVersion 23
        minSdkVersion 10 //Android 2.3.3
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = (bintrayUsername != null ? bintrayUsername : "")
    key = (bintrayKey != null ? bintrayKey : "")

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = 'com.aware.phone'
        licenses = ['Apache-2.0']
        websiteUrl = 'http://www.awareframework.com'
        issueTrackerUrl = 'https://github.com/denzilferreira/aware-client/issues'
        labels = ['aar','android','framework','aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/denzilferreira/aware-client.git'
        desc = "AWARE Framework"
        userOrg = user
        version {
            name = "$version_readable"
            desc = "AWARE for Android"
            released = new Date()
            vcsTag = "$version_readable"
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

dependencies {
    compile project(':aware-core')
    compile "com.google.android.gms:play-services-vision:$google_libs"
    compile "com.android.support:cardview-v7:$support_libs"
    compile "com.android.support:appcompat-v7:$support_libs"
    compile "com.android.support:design:$support_libs"
}

